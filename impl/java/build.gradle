import static org.gradle.api.JavaVersion.VERSION_1_7

buildscript {
    ext.kotlin_version = '1.3.72'

    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.novoda:bintray-release:0.9.2'
    }
}


allprojects {
    ext {
        publish_version = '2.8.3'
    }
    tasks.whenTaskAdded {
        if (name == "generateSourcesJarForMavenPublication") {
            it as Jar
            from(sourceSets.main.allSource)
        }
    }
}

subprojects {
    apply plugin: 'kotlin'
    apply plugin: 'com.novoda.bintray-release'
    apply plugin: 'maven-publish'

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }

    group 'br.com.guiabolso'
    sourceCompatibility = VERSION_1_7
    targetCompatibility = VERSION_1_7

    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
        implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

        implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'

        testImplementation "ch.qos.logback:logback-classic:1.2.3"
        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.2.0'
        testRuntime group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.2.0'
        testImplementation group: 'org.mockito', name: 'mockito-core', version: '2.18.3'
        testImplementation group: 'com.nhaarman', name: 'mockito-kotlin', version: '1.5.0'
    }

    test {
        useJUnitPlatform()
    }

    compileKotlin {
        sourceCompatibility = VERSION_1_7
        targetCompatibility = VERSION_1_7
        kotlinOptions {
            jvmTarget = '1.6'
            allWarningsAsErrors = true
        }
    }

}
